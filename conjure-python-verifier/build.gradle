/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 */

import static org.apache.tools.ant.taskdefs.condition.Os.*

ext {
    osClassifier = isFamily(FAMILY_MAC) ? "osx" : "linux"
}

configurations {
    testCases
    verificationApi
    verificationServer
}

dependencies {
    testCases 'com.palantir.conjure.verification:test-cases'
    verificationApi 'com.palantir.conjure.verification:verification-api'
    verificationServer "com.palantir.conjure.verification:verification-server::${osClassifier}@tgz"
}

task unpackVerificationServer(type: Sync) {
    from { tarTree(configurations.verificationServer.singleFile) }
    into "${buildDir}/verification-server"
    rename { "server" }
}

task copyTestCases(type: Sync) {
    from configurations.testCases
    into "$buildDir/test-cases"
    rename { "test-cases.json" }
}

task generateVerifierBindings(type: JavaExec, dependsOn: [copyTestCases, ':conjure-python:compileJava']) {
    main = "com.palantir.conjure.python.cli.ConjurePythonCli"
    classpath = project(':conjure-python').sourceSets.main.runtimeClasspath
    args 'generate',
            "${-> configurations.verificationApi.singleFile}",
            'python/test',
            '--packageName', 'generated',
            '--packageVersion', '0.0.0'

    inputs.file configurations.verificationApi.singleFile
    doLast {delete "python/test/setup.py"}
}

tasks.idea.dependsOn unpackVerificationServer, generateVerifierBindings
tasks.build.dependsOn unpackVerificationServer, generateVerifierBindings
