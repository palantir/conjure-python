/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.palantir.external-publish-application-dist'

mainClassName = 'com.palantir.conjure.python.cli.ConjurePythonCli'

dependencies {
    implementation project(':conjure-python-core')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.google.guava:guava'
    implementation 'com.palantir.conjure:conjure-generator-common'
    implementation 'info.picocli:picocli'
    runtimeOnly 'org.slf4j:slf4j-simple'

    testImplementation 'junit:junit'
    testImplementation 'org.assertj:assertj-core'

    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value::annotations'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine', {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
}

task generateBuildConfiguration {
    doLast {
        def minConjureClientVersion = conjurePythonClientVersion
        def props = new File("$projectDir/src/main/resources/buildConfiguration.yml")
        props.getParentFile().mkdirs()
        props.text = "generatorVersion: ${project.version}\n"
        props.text += "minConjureClientVersion: $minConjureClientVersion\n"
    }
}

compileJava.dependsOn generateBuildConfiguration
